Sub VBA()
    Dim i As Long
    Dim lastrow As Long
    Dim ws As Worksheet
    Dim Ticker As String
    Dim Summary_Table_Row As Integer
    Dim QuarterlyChange As Double
    Dim PercentChange As Double
    Dim OpeningPrice As Double
    Dim ClosingPrice As Double
    Dim TotalVolume As Double
    Dim GreatestIncrease As String
    Dim GreatestDecrease As String
    Dim GreatestTotalVolume As Double
    Dim maxVal As Double
    Dim MinVal As Double
    
    For Each ws In ThisWorkbook.Worksheets
        ' Determine the last row of data
        lastrow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
        
        ' Set headers
        ws.Range("I1").Value = "Ticker"
        ws.Range("J1").Value = "Quarterly Change"
        ws.Range("K1").Value = "Percent Change"
        ws.Range("L1").Value = "Total Stock Volume"
        ws.Range("P1").Value = "Ticker"
        ws.Range("Q1").Value = "Value"
        ws.Range("O2").Value = "Greatest % Increase"
        ws.Range("O3").Value = "Greatest % Decrease"
        ws.Range("O4").Value = "Greatest Total Volume"
        
        ' Initialize variables
        TotalVolume = 0
        OpeningPrice = ws.Cells(2, 3).Value
        Summary_Table_Row = 2
        
        For i = 2 To lastrow
            ' Accumulate the total volume
            TotalVolume = TotalVolume + ws.Cells(i, 7).Value
            
            ' Check if we have reached the end of a ticker's data
            If ws.Cells(i + 1, 1).Value <> ws.Cells(i, 1).Value Then
                ' Set the ticker name
                Ticker = ws.Cells(i, 1).Value
                ws.Range("I" & Summary_Table_Row).Value = Ticker
                
                ' Set the closing price
                ClosingPrice = ws.Cells(i, 6).Value
                
                ' Calculate the quarterly change
                QuarterlyChange = ClosingPrice - OpeningPrice
                ws.Range("J" & Summary_Table_Row).Value = QuarterlyChange
                
                ' Calculate the percent change
                If OpeningPrice <> 0 Then
                    PercentChange = QuarterlyChange / OpeningPrice
                Else
                    PercentChange = 0
                End If
                ws.Range("K" & Summary_Table_Row).Value = PercentChange
                ws.Range("K" & Summary_Table_Row).NumberFormat = "0.00%"
                
                ' Set the total volume
                ws.Range("L" & Summary_Table_Row).Value = TotalVolume
                
                ' Move to the next summary table row
                Summary_Table_Row = Summary_Table_Row + 1
                
                ' Reset variables for the next ticker
                TotalVolume = 0
                If i + 1 <= lastrow Then
                    OpeningPrice = ws.Cells(i + 1, 3).Value
                End If
            End If
        Next i
        
        ' Calculate and set greatest increase, decrease, and total volume
        maxVal = Application.WorksheetFunction.Max(ws.Range("K2:K" & lastrow))
        MinVal = Application.WorksheetFunction.Min(ws.Range("K2:K" & lastrow))
        GreatestTotalVolume = Application.WorksheetFunction.Max(ws.Range("L2:L" & lastrow))
        
        ws.Range("Q2").Value = maxVal
        ws.Range("Q3").Value = MinVal
        ws.Range("Q4").Value = GreatestTotalVolume
        
        ws.Range("P2").FormulaR1C1 = "=INDEX(C[-7], MATCH(MAX(C[-5]), C[-5], 0))"
        ws.Range("P3").FormulaR1C1 = "=INDEX(C[-7], MATCH(MIN(C[-5]), C[-5], 0))"
        ws.Range("P4").FormulaR1C1 = "=INDEX(C[-7], MATCH(MAX(C[-4]), C[-4], 0))"
        
        ' Color cells in column J based on their value
        For i = 2 To lastrow
            If ws.Cells(i, "J").Value < 0 Then
                ws.Cells(i, "J").Interior.Color = vbRed
            ElseIf ws.Cells(i, "J").Value > 0 Then
                ws.Cells(i, "J").Interior.Color = vbGreen
            Else
                ws.Cells(i, "J").Interior.ColorIndex = xlNone ' No fill for zero values
            End If
        Next i
    Next ws
End Sub
